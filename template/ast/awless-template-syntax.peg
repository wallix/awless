# PEG for awless template syntax
package ast

type Peg Peg {
 *AST
}

Script   <- Spacing Statement+ EndOfFile
Statement <- Spacing (Expr / Declaration / Comment) Spacing EndOfLine*
Action <- 'create' / 'delete' / 'start' / 'stop' / 'update' / 'attach' / 'check' / 'detach'
Entity <- 'vpc' / 'subnet' / 'instance' / 'volume' / 'tag' / 'user' / 'group' / 'role' / 'policy' / 'keypair' / 'securitygroup' / 'internetgateway' / 'routetable' / 'route' / 'bucket' / 'storageobject' / 'subscription' / 'topic' / 'queue'
Declaration <- <Identifier> { p.addDeclarationIdentifier(text) }
               Equal
               Expr
Expr <- <Action> { p.addAction(text) }
        MustWhiteSpacing <Entity> { p.addEntity(text) }
        (MustWhiteSpacing Params)? { p.LineDone() }

Params <- Param+
Param <- <Identifier> { p.addParamKey(text) }
         Equal
         Value
         WhiteSpacing

Identifier <- [a-zA-Z0-9-_.]+

Value <- HoleValue {  p.addParamHoleValue(text) }
        / AliasValue {  p.addParamAliasValue(text) }
        / RefValue {  p.addParamRefValue(text) }
        / <CidrValue> { p.addParamCidrValue(text) }
        / <IpValue> { p.addParamIpValue(text) }
        / <IntRangeValue> { p.addParamValue(text) }
        / <IntValue> { p.addParamIntValue(text) }
        / <StringValue> { p.addParamValue(text) }


StringValue <- [a-zA-Z0-9-._:/]+

CidrValue <- [0-9]+.[0-9]+.[0-9]+.[0-9]+'/'[0-9]+
IpValue <- [0-9]+.[0-9]+.[0-9]+.[0-9]+
IntValue <- [0-9]+
IntRangeValue <- [0-9]+'-'[0-9]+

RefValue <- '$'<Identifier>
AliasValue <- '@'<StringValue>
HoleValue <- '{'WhiteSpacing<Identifier>WhiteSpacing'}'

Comment <- '#'(!EndOfLine .)* / '//'(!EndOfLine .)* { p.LineDone() }

Spacing <- Space*
WhiteSpacing <- Whitespace*
MustWhiteSpacing <- Whitespace+
Equal <- Spacing '=' Spacing
Space   <- Whitespace / EndOfLine
Whitespace   <- ' ' / '\t'
EndOfLine <- '\r\n' / '\n' / '\r'
EndOfFile <- !.
